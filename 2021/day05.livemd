<!-- livebook:{"persist_outputs":true} -->

# 2021 / Day 5: Hydrothermal Venturebook

## Puzzle

```elixir
puzzle =
  "./puzzles/day05.txt"
  |> Path.expand(__DIR__)
  |> File.read!()
  |> String.trim()
  |> String.split("\n")

puzzle =
  Enum.map(puzzle, fn row ->
    [[_, x1, y1, x2, y2]] = Regex.scan(~r/(\d+),(\d+) -> (\d+),(\d+)/, row)
    {String.to_integer(x1), String.to_integer(y1), String.to_integer(x2), String.to_integer(y2)}
  end)
```

```output
[
  {941, 230, 322, 849},
  {762, 196, 701, 257},
  {656, 197, 595, 136},
  {687, 692, 57, 692},
  {37, 953, 903, 87},
  {674, 102, 84, 102},
  {952, 323, 786, 157},
  {807, 948, 430, 948},
  {280, 66, 514, 66},
  {810, 381, 928, 263},
  {41, 278, 112, 207},
  {754, 11, 754, 574},
  {499, 830, 725, 604},
  {713, 172, 658, 172},
  {805, 54, 594, 54},
  {442, 910, 40, 508},
  {160, 170, 925, 935},
  {265, 899, 265, 313},
  {960, 976, 77, 93},
  {820, 244, 877, 187},
  {883, 501, 345, 501},
  {12, 978, 941, 49},
  {988, 46, 988, 572},
  {285, 775, 285, 298},
  {718, 69, 121, 69},
  {218, 641, 146, 641},
  {857, 277, 124, 277},
  {32, 36, 657, 36},
  {964, 280, 609, 280},
  {739, 981, 910, 981},
  {960, 794, 243, 794},
  {447, 682, 751, 378},
  {813, 103, 813, 240},
  {568, 705, 497, 705},
  {888, 47, 888, 231},
  {936, 95, 336, 695},
  {305, 349, 18, 636},
  {54, 240, 54, 222},
  {28, 704, 625, 107},
  {680, 325, 680, 623},
  {209, 405, 209, 123},
  {947, 906, 947, 721},
  {149, 810, 834, 125},
  {897, 875, 146, 124},
  {928, 267, 928, 484},
  {871, 516, 871, 136},
  {954, 725, 706, ...},
  {680, 645, ...},
  {680, ...},
  {...},
  ...
]
```

## Part 1

You come across a field of hydrothermal vents on the ocean floor! These vents constantly produce large, opaque clouds, so it would be best to avoid them if possible.

They tend to form in lines; the submarine helpfully produces a list of nearby lines of vents (your puzzle input) for you to review. For example:

```
0,9 -> 5,9
8,0 -> 0,8
9,4 -> 3,4
2,2 -> 2,1
7,0 -> 7,4
6,4 -> 2,0
0,9 -> 2,9
3,4 -> 1,4
0,0 -> 8,8
5,5 -> 8,2
```

Each line of vents is given as a line segment in the format x1,y1 -> x2,y2 where x1,y1 are the coordinates of one end the line segment and x2,y2 are the coordinates of the other end. These line segments include the points at both ends. In other words:

* An entry like 1,1 -> 1,3 covers points 1,1, 1,2, and 1,3.
* An entry like 9,7 -> 7,7 covers points 9,7, 8,7, and 7,7.
  For now, only consider horizontal and vertical lines: lines where either `x1 = x2` or `y1 = y2`.

So, the horizontal and vertical lines from the above list would produce the following diagram:

```
.......1..
..1....1..
..1....1..
.......1..
.112111211
..........
..........
..........
..........
222111....
```

In this diagram, the top left corner is `0,0` and the bottom right corner is `9,9`. Each position is shown as the number of lines which cover that point or `.` if no line covers that point. The top-left pair of 1s, for example, comes from `2,2 -> 2,1`; the very bottom row is formed by the overlapping lines `0,9 -> 5,9` and `0,9 -> 2,9`.

To avoid the most dangerous areas, you need to determine the number of points where at least two lines overlap. In the above example, this is anywhere in the diagram with a 2 or larger - a total of 5 points.

Consider only horizontal and vertical lines. At how many points do at least two lines overlap?

```elixir
grid_with_vertical_rows =
  puzzle
  |> Enum.filter(fn {x1, _y1, x2, _y2} -> x1 == x2 end)
  |> Enum.flat_map(fn {x, y1, x, y2} ->
    Enum.map(y1..y2, &{x, &1})
  end)
  |> Enum.reduce(%{}, fn {x, y}, grid ->
    Map.update(grid, {x, y}, 1, &(&1 + 1))
  end)

grid_with_horizontal_and_vertical_rows =
  puzzle
  |> Enum.filter(fn {_x1, y1, _x2, y2} -> y1 == y2 end)
  |> Enum.flat_map(fn {x1, y, x2, y} ->
    Enum.map(x1..x2, &{&1, y})
  end)
  |> Enum.reduce(grid_with_vertical_rows, fn {x, y}, grid ->
    Map.update(grid, {x, y}, 1, &(&1 + 1))
  end)

grid_with_horizontal_and_vertical_rows |> Map.values() |> Enum.count(&(&1 > 1))
```

```output
5585
```

## Part 2

Unfortunately, considering only horizontal and vertical lines doesn't give you the full picture; you need to also consider diagonal lines.

Because of the limits of the hydrothermal vent mapping system, the lines in your list will only ever be horizontal, vertical, or a diagonal line at exactly 45 degrees. In other words:

An entry like 1,1 -> 3,3 covers points 1,1, 2,2, and 3,3.
An entry like 9,7 -> 7,9 covers points 9,7, 8,8, and 7,9.
Considering all lines from the above example would now produce the following diagram:

```
1.1....11.
.111...2..
..2.1.111.
...1.2.2..
.112313211
...1.2....
..1...1...
.1.....1..
1.......1.
222111....
```

You still need to determine the number of points where at least two lines overlap. In the above example, this is still anywhere in the diagram with a 2 or larger - now a total of 12 points.

Consider all of the lines. At how many points do at least two lines overlap?

```elixir
grid_with_all_rows =
  puzzle
  |> Enum.filter(fn {x1, y1, x2, y2} -> x1 != x2 and y1 != y2 end)
  |> Enum.flat_map(fn {x1, y1, x2, y2} ->
    Enum.zip(x1..x2, y1..y2)
  end)
  |> Enum.reduce(grid_with_horizontal_and_vertical_rows, fn {x, y}, grid ->
    Map.update(grid, {x, y}, 1, &(&1 + 1))
  end)

grid_with_all_rows |> Map.values() |> Enum.count(&(&1 > 1))
```

```output
17193
```
