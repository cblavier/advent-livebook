# 2016 / Day 2: Bathroom Security

## Puzzle

```elixir
puzzle = "./puzzles/day02.txt" |> Path.expand(__DIR__) |> File.read!()
instructions = puzzle |> String.split("\n") |> Enum.slice(0..-2)
```

## Part1

You arrive at Easter Bunny Headquarters under cover of darkness. However, you left in such a rush that you forgot to use the bathroom! Fancy office buildings like this one usually have keypad locks on their bathrooms, so you search the front desk for the code.

"In order to improve security," the document you find says, "bathroom codes will no longer be written down. Instead, please memorize and follow the procedure below to access the bathrooms."

The document goes on to explain that each button to be pressed can be found by starting on the previous button and moving to adjacent buttons on the keypad: U moves up, D moves down, L moves left, and R moves right. Each line of instructions corresponds to one button, starting at the previous button (or, for the first line, the "5" button); press whatever button you're on at the end of each line. If a move doesn't lead to a button, ignore it.

You can't hold it much longer, so you decide to figure out the code as you walk to the bathroom. You picture a keypad like this:

```
1 2 3
4 5 6
7 8 9
```

Suppose your instructions are:

```
ULL
RRDDD
LURDL
UUUUD
```

* You start at "5" and move up (to "2"), left (to "1"), and left (you can't, and stay on "1"), so the first button is 1.
* Starting from the previous button ("1"), you move right twice (to "3") and then down three times (stopping at "9" after two moves and ignoring the third), ending up with 9.
* Continuing from "9", you move left, up, right, down, and left, ending with 8.
* Finally, you move up four times (stopping at "2"), then down once, ending with 5.

So, in this example, the bathroom code is **1985**.

Your puzzle input is the instructions from the document you found at the front desk.

**What is the bathroom code?**

```elixir
defmodule KeypadReducer do
  def run(instructions, initial_position, evaluator) do
    instructions
    |> Enum.reduce({initial_position, []}, fn instruction, {position, code} ->
      new_position = evaluator.run_instruction(instruction, position)
      {new_position, code ++ [evaluator.key_value(new_position)]}
    end)
    |> elem(1)
    |> Enum.join()
  end
end

defmodule Part1KeypadEvaluator do
  def run_instruction(instruction, position) do
    instruction
    |> String.graphemes()
    |> Enum.reduce(position, fn
      "U", {x, y} -> {x, max(y - 1, 0)}
      "R", {x, y} -> {min(x + 1, 2), y}
      "D", {x, y} -> {x, min(y + 1, 2)}
      "L", {x, y} -> {max(x - 1, 0), y}
    end)
  end

  def key_value({0, 0}), do: "1"
  def key_value({1, 0}), do: "2"
  def key_value({2, 0}), do: "3"
  def key_value({0, 1}), do: "4"
  def key_value({1, 1}), do: "5"
  def key_value({2, 1}), do: "6"
  def key_value({0, 2}), do: "7"
  def key_value({1, 2}), do: "8"
  def key_value({2, 2}), do: "9"
end

KeypadReducer.run(instructions, {1, 1}, Part1KeypadEvaluator)
```

## Part2

You finally arrive at the bathroom (it's a several minute walk from the lobby so visitors can behold the many fancy conference rooms and water coolers on this floor) and go to punch in the code. Much to your bladder's dismay, the keypad is not at all like you imagined it. Instead, you are confronted with the result of hundreds of man-hours of bathroom-keypad-design meetings:

```
    1
  2 3 4
5 6 7 8 9
  A B C
    D
```

You still start at "5" and stop when you're at an edge, but given the same instructions as above, the outcome is very different:

* You start at "5" and don't move at all (up and left are both edges), ending at 5.
* Continuing from "5", you move right twice and down three times (through "6", "7", "B", "D", "D"), ending at D.
* Then, from "D", you move five more times (through "D", "B", "C", "C", "B"), ending at B.
* Finally, after five more moves, you end at `3`.
  So, given the actual keypad layout, the code would be `5DB3`.

Using the same instructions in your puzzle input, what is the correct __bathroom code__?

```elixir
defmodule Part2KeypadEvaluator do
  def run_instruction(instruction, position) do
    instruction
    |> String.graphemes()
    |> Enum.reduce(position, fn
      "U", {x, y} when x in [0, 4] -> {x, y}
      "U", {x, y} when x in [1, 3] -> {x, max(y - 1, 1)}
      "U", {x, y} when x == 2 -> {x, max(y - 1, 0)}
      "D", {x, y} when x in [0, 4] -> {x, y}
      "D", {x, y} when x in [1, 3] -> {x, min(y + 1, 3)}
      "D", {x, y} when x == 2 -> {x, min(y + 1, 4)}
      "L", {x, y} when y in [0, 4] -> {x, y}
      "L", {x, y} when y in [1, 3] -> {max(x - 1, 1), y}
      "L", {x, y} when y == 2 -> {max(x - 1, 0), y}
      "R", {x, y} when y in [0, 4] -> {x, y}
      "R", {x, y} when y in [1, 3] -> {min(x + 1, 3), y}
      "R", {x, y} when y == 2 -> {min(x + 1, 4), y}
    end)
  end

  def key_value({2, 0}), do: "1"
  def key_value({1, 1}), do: "2"
  def key_value({2, 1}), do: "3"
  def key_value({3, 1}), do: "4"
  def key_value({0, 2}), do: "5"
  def key_value({1, 2}), do: "6"
  def key_value({2, 2}), do: "7"
  def key_value({3, 2}), do: "8"
  def key_value({4, 2}), do: "9"
  def key_value({1, 3}), do: "A"
  def key_value({2, 3}), do: "B"
  def key_value({3, 3}), do: "C"
  def key_value({2, 4}), do: "D"
end

KeypadReducer.run(instructions, {0, 2}, Part2KeypadEvaluator)
```
