# 2016 / Day 5: How About a Nice Game of Chess?

## Puzzle

```elixir
puzzle = "ugkcyxxp"
```

## Part1

You are faced with a security door designed by Easter Bunny engineers that seem to have acquired most of their security knowledge by watching hacking movies.

The __eight-character password__ for the door is generated one character at a time by finding the MD5 hash of some Door ID (your puzzle input) and an increasing integer index (starting with `0`).

A hash indicates the next character in the password if its hexadecimal representation starts with five zeroes. If it does, the sixth character in the hash is the next character of the password.

For example, if the Door ID is `abc`:

* The first index which produces a hash that starts with five zeroes is `3231929`, which we find by hashing `abc3231929`; the sixth character of the hash, and thus the first character of the password, is `1`.
* `5017308` produces the next interesting hash, which starts with `000008f82`..., so the second character of the password is `8`.
* The third time a hash starts with five zeroes is for `abc5278568`, discovering the character `f`.

In this example, after continuing this search a total of eight times, the password is `18f47a30`.

Given the actual Door ID, __what is the password?__

```elixir
defmodule Hash do
  def hash(word), do: :crypto.hash(:md5, word) |> Base.encode16()
end

defmodule PasswordFinder do
  def find(door_id, sequence \\ 0, password \\ [])
  def find(_, _, pwd) when length(pwd) == 8, do: Enum.join(pwd)

  def find(door_id, sequence, pwd) do
    case Hash.hash("#{door_id}#{sequence}") do
      <<?0, ?0, ?0, ?0, ?0, c, _rest::binary>> ->
        find(door_id, sequence + 1, pwd ++ [<<c>>])

      _ ->
        find(door_id, sequence + 1, pwd)
    end
  end
end

PasswordFinder.find(puzzle)
```

## Part 2

As the door slides open, you are presented with a second door that uses a slightly more inspired security mechanism. Clearly unimpressed by the last version (in what movie is the password decrypted in order?!), the Easter Bunny engineers have worked out a __better solution__.

Instead of simply filling in the password from left to right, the hash now also indicates the position within the password to fill. You still look for hashes that begin with five zeroes; however, now, the __sixth__ character represents the __position__ (`0`-`7`), and the seventh character is the character to put in that position.

A hash result of `000001f` means that `f` is the __second__ character in the password. Use only the __first result__ for each position, and ignore invalid positions.

For example, if the Door ID is `abc`:

* The first interesting hash is from `abc3231929`, which produces `0000015...`; so, `5` goes in position `1`: `_5______`.
* In the previous method, `5017308` produced an interesting hash; however, it is ignored, because it specifies an invalid position (`8`).
* The second interesting hash is at index `5357525`, which produces `000004e...`; so, `e` goes in position `4`: `_5__e___`.

You almost choke on your popcorn as the final character falls into place, producing the password `05ace8e3`.

Given the actual Door ID and this new method, __what is the password__? Be extra proud of your solution if it uses a cinematic "decrypting" animation.

```elixir
defmodule PasswordFinder do
  def find(door_id, sequence \\ 0, password \\ %{})

  def find(_, _, %{0 => a, 1 => b, 2 => c, 3 => d, 4 => e, 5 => f, 6 => g, 7 => h}) do
    Enum.join([a, b, c, d, e, f, g, h])
  end

  def find(door_id, sequence, pwd) do
    case Hash.hash("#{door_id}#{sequence}") do
      <<?0, ?0, ?0, ?0, ?0, pos, c, _rest::binary>> ->
        if pos - ?0 < 8 do
          find(door_id, sequence + 1, Map.update(pwd, pos - ?0, <<c>>, & &1))
        else
          find(door_id, sequence + 1, pwd)
        end

      _ ->
        find(door_id, sequence + 1, pwd)
    end
  end
end

PasswordFinder.find(puzzle)
```
