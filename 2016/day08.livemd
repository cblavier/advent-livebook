<!-- livebook:{"persist_outputs":true} -->

# 2016 / Day 8: Two-Factor Authentication

## Puzzle

```elixir
puzzle =
  "./puzzles/day08.txt"
  |> Path.expand(__DIR__)
  |> File.read!()
  |> String.trim()
  |> String.split("\n")
  |> Enum.map(fn
    "rect " <> rest ->
      [_, x, y] = Regex.run(~r/(\d+)x(\d+)/, rest)
      {:rect, String.to_integer(x), String.to_integer(y)}

    "rotate row " <> rest ->
      [_, row, size] = Regex.run(~r/y=(\d+) by (\d+)/, rest)
      {:rotate_row, String.to_integer(row), String.to_integer(size)}

    "rotate column " <> rest ->
      [_, column, size] = Regex.run(~r/x=(\d+) by (\d+)/, rest)
      {:rotate_column, String.to_integer(column), String.to_integer(size)}
  end)

:ok
```

```output
:ok
```

## Part 1

You come across a door implementing what you can only assume is an implementation of two-factor authentication after a long game of requirements telephone.

To get past the door, you first swipe a keycard (no problem; there was one on a nearby desk). Then, it displays a code on a little screen, and you type that code on a keypad. Then, presumably, the door unlocks.

Unfortunately, the screen has been smashed. After a few minutes, you've taken everything apart and figured out how it works. Now you just have to work out what the screen would have displayed.

The magnetic strip on the card you swiped encodes a series of instructions for the screen; these instructions are your puzzle input. The screen is __50 pixels wide and 6 pixels tall__, all of which start off, and is capable of three somewhat peculiar operations:

* `rect AxB` turns on all of the pixels in a rectangle at the top-left of the screen which is `A` wide and `B` tall.
* `rotate row y=A by B` shifts all of the pixels in row `A` (`0` is the top row) right by B pixels. Pixels that would fall off the right end appear at the left end of the row.
* `rotate column x=A by B` shifts all of the pixels in column `A` (`0` is the left column) down by `B` pixels. Pixels that would fall off the bottom appear at the top of the column.

For example, here is a simple sequence on a smaller screen:

`rect 3x2` creates a small rectangle in the top-left corner

```
###....
###....
.......
```

`rotate column x=1 by 1` rotates the second column down by one pixel:

```
#.#....
###....
.#.....
```

`rotate row y=0 by 4` rotates the top row right by four pixels:

```
....#.#
###....
.#.....
```

`rotate column x=1 by 1` again rotates the second column down by one pixel, causing the bottom pixel to wrap back to the top:

```
.#..#.#
#.#....
.#.....
```

As you can see, this display technology is extremely powerful, and will soon dominate the tiny-code-displaying-screen market. That's what the advertisement on the back of the display tries to convince you, anyway.

There seems to be an intermediate check of the voltage used by the display: after you swipe your card, if the screen did work, how many pixels should be lit?

```elixir
screen_width = 50
screen_height = 6

screen =
  Enum.reduce(puzzle, %{}, fn
    {:rect, width, height}, acc ->
      for x <- 0..(width - 1), y <- 0..(height - 1), reduce: acc do
        acc -> Map.put(acc, {x, y}, true)
      end

    {:rotate_row, row, size}, acc ->
      row_before = for x <- 0..(screen_width - 1), do: Map.get(acc, {x, row})
      row_after = Enum.slide(row_before, 0..(screen_width - 1 - size), -1)

      for x <- 0..(screen_width - 1), reduce: acc do
        acc -> Map.put(acc, {x, row}, Enum.at(row_after, x))
      end

    {:rotate_column, column, size}, acc ->
      col_before = for y <- 0..(screen_height - 1), do: Map.get(acc, {column, y})
      col_after = Enum.slide(col_before, 0..(screen_height - 1 - size), -1)

      for y <- 0..(screen_height - 1), reduce: acc do
        acc -> Map.put(acc, {column, y}, Enum.at(col_after, y))
      end
  end)

screen |> Map.values() |> Enum.filter(& &1) |> Enum.count()
```

```output
115
```

## Part 2

You notice that the screen is only capable of displaying capital letters; in the font it uses, each letter is `5` pixels wide and `6` tall.

After you swipe your card, __what code is the screen trying to display?__

```elixir
for y <- 0..(screen_height - 1) do
  for x <- 0..(screen_width - 1), into: "" do
    case Map.get(screen, {x, y}) do
      true -> "#"
      _ -> " "
    end
  end
end
```

```output
["#### #### #### #   ##  # #### ###  ####  ###   ## ",
 "#    #    #    #   ## #  #    #  # #      #     # ",
 "###  ###  ###   # # ##   ###  #  # ###    #     # ",
 "#    #    #      #  # #  #    ###  #      #     # ",
 "#    #    #      #  # #  #    # #  #      #  #  # ",
 "#### #    ####   #  #  # #    #  # #     ###  ##  "]
```
