# 2016 / Day 01 : No Time for a Taxicab

## Puzzle

https://adventofcode.com/2016/day/1

```elixir
puzzle = "./puzzles/day01.txt" |> Path.expand(__DIR__) |> File.read!()

instructions =
  puzzle
  |> String.split(",")
  |> Enum.map(&String.trim/1)
  |> Enum.map(
    &{
      String.at(&1, 0),
      &1 |> String.slice(1..-1) |> String.to_integer()
    }
  )
```

## Part1

Santa's sleigh uses a very high-precision clock to guide its movements, and the clock's oscillator is regulated by stars. Unfortunately, the stars have been stolen... by the Easter Bunny. To save Christmas, Santa needs you to retrieve all fifty stars by December 25th.

Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!

You're airdropped near Easter Bunny Headquarters in a city somewhere. "Near", unfortunately, is as close as you can get - the instructions on the Easter Bunny Recruiting Document the Elves intercepted start here, and nobody had time to work them out further.

The Document indicates that you should start at the given coordinates (where you just landed) and face North. Then, follow the provided sequence: either turn left (L) or right (R) 90 degrees, then walk forward the given number of blocks, ending at a new intersection.

There's no time to follow such ridiculous instructions on foot, though, so you take a moment and work out the destination. Given that you can only walk on the [street grid of the city](https://en.wikipedia.org/wiki/Taxicab_geometry), how far is the shortest path to the destination?

For example:

* Following R2, L3 leaves you 2 blocks East and 3 blocks North, or 5 blocks away.
* R2, R2, R2 leaves you 2 blocks due South of your starting position, which is 2 blocks away.
* R5, L5, R5, R3 leaves you 12 blocks away.

**How many blocks away is Easter Bunny HQ?**

```elixir
{_direction, x, y} =
  Enum.reduce(instructions, {:up, 0, 0}, fn
    {"R", distance}, {:up, x, y} -> {:right, x + distance, y}
    {"R", distance}, {:down, x, y} -> {:left, x - distance, y}
    {"R", distance}, {:right, x, y} -> {:down, x, y - distance}
    {"R", distance}, {:left, x, y} -> {:up, x, y + distance}
    {"L", distance}, {:up, x, y} -> {:left, x - distance, y}
    {"L", distance}, {:down, x, y} -> {:right, x + distance, y}
    {"L", distance}, {:right, x, y} -> {:up, x, y + distance}
    {"L", distance}, {:left, x, y} -> {:down, x, y - distance}
  end)

abs(x) + abs(y)
```

## Part2

Then, you notice the instructions continue on the back of the Recruiting Document. Easter Bunny HQ is actually at the first location you visit twice.

For example, if your instructions are R8, R4, R4, R8, the first location you visit twice is 4 blocks away, due East.

How many blocks away is **the first location you visit twice?**

---

<!-- livebook:{"break_markdown":true} -->

_First I start by computing all successive positions_

```elixir
{_direction, all_positions} =
  Enum.reduce(instructions, {:up, [{0, 0}]}, fn
    {"R", distance}, {:up, [{x, y} | visited]} ->
      {:right, Enum.map((x + distance)..x, &{&1, y}) ++ visited}

    {"R", distance}, {:down, [{x, y} | visited]} ->
      {:left, Enum.map((x - distance)..x, &{&1, y}) ++ visited}

    {"R", distance}, {:right, [{x, y} | visited]} ->
      {:down, Enum.map((y - distance)..y, &{x, &1}) ++ visited}

    {"R", distance}, {:left, [{x, y} | visited]} ->
      {:up, Enum.map((y + distance)..y, &{x, &1}) ++ visited}

    {"L", distance}, {:up, [{x, y} | visited]} ->
      {:left, Enum.map((x - distance)..x, &{&1, y}) ++ visited}

    {"L", distance}, {:down, [{x, y} | visited]} ->
      {:right, Enum.map((x + distance)..x, &{&1, y}) ++ visited}

    {"L", distance}, {:right, [{x, y} | visited]} ->
      {:up, Enum.map((y + distance)..y, &{x, &1}) ++ visited}

    {"L", distance}, {:left, [{x, y} | visited]} ->
      {:down, Enum.map((y - distance)..y, &{x, &1}) ++ visited}
  end)

all_positions
```

_Then just find the first duplicate_

```elixir
{x, y} =
  all_positions
  |> Enum.reverse()
  |> Enum.reduce_while([], fn position, visited ->
    if position in visited do
      {:halt, position}
    else
      {:cont, [position | visited]}
    end
  end)

abs(x) + abs(y)
```
